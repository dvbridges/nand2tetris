// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
        // Put you code here:
        // Zero output
        Mux16(a=x, sel=zx, out=zerodX);
        Mux16(a=y, sel=zy, out=zerodY);

        // Negate output
        Not16(in=zerodX, out=negatedX);
        Not16(in=zerodY, out=negatedY);
        
        // Return x,y
        Mux16(a=zerodX, b=negatedX, sel=nx, out=nxOut);
        Mux16(a=zerodY, b=negatedY, sel=ny, out=nyOut);

        // Calulate f values
        And16(a=nxOut, b=nyOut, out=XandY);
        Add16(a=nxOut, b=nyOut, out=XaddY);

        // Select f values
        Mux16(a=XandY, b=XaddY, sel=f, out=fOut);
        Not16(in=fOut, out=notOut);

        // Return final vals
        // firstOut: positive sign magnitude numbers always start with 0, neg sign start with 1.
        Mux16(a=fOut, b=notOut, sel=no, out=out, out[15]=firstOut, out[0..7]=finalLeft, out[8..15]=finalRight);
        
        // Calculate zr
        Or8Way(in=finalLeft, out=zr1);
        Or8Way(in=finalRight, out=zr2);
        Or(a=zr1, b=zr2, out=zrFin);
        Not(in=zrFin, out=zr);

        // Calculate ng
        And(a=firstOut, b=true, out=ng);
}